plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.0' // Replace with the appropriate version
    id "com.diffplug.spotless" version "6.25.0"
    id 'checkstyle'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

allprojects {
    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion = '10.18.0'
        configFile = file("${rootProject.projectDir}/config/checkstyle/google_checks.xml")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
}

tasks.test {
    useJUnitPlatform()
}

tasks.register('addPreCommitGitHookOnBuild', Copy) {
    doFirst {
        println "⚈ ⚈ ⚈ Running Add Pre Commit Git Hook Script on Build ⚈ ⚈ ⚈"
    }

    from '.scripts/'
    into '.git/hooks/'
    include 'pre-commit'

    doLast {
        println "Pre-Commit file copied successfully to .git/hooks!"
    }
}

tasks.register('runScript', Exec) {
    if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        commandLine 'sh', 'setup-hooks.sh'
    } else {
        commandLine 'cmd', '/c', 'echo Running on Windows, no script executed.'
    }
}


tasks.named('build'){
    dependsOn 'runScript'
    dependsOn 'addPreCommitGitHookOnBuild'
}

